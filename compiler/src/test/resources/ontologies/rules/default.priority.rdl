#
# this simulates OWLIM's owl-horst_partialRDFS_builtin.pie rule file that builds
# on the rule semantics for OWL as published by ter Horst which is based on Hayes'
# work for axiomatizing RDF Schema;
# I have split up rule $owl_FunctProp into $owl_FunctPropObject and $owl_FunctPropDatatype,
# since the nature of the object of the triple either leads to a owl:sameAs statement or to
# the bottom type owl:Nothing
#
# @author Hans-Ulrich Krieger
#


# NOTE: $rdf1_rdfs4a_4b (= rdf1 & rdfs4a & rdfs4b) modified;
# not interested that ?s, ?p, and ?o are all of rdf:type rdfs:Resource;
# only add that ?p is of rdf:type rdf:Property
$rdf1_rdfs4a_4b
&priority 140
?_s ?p ?_o
->
?p <rdf:type> <rdf:Property>


$rdfs2
&priority 80
?s ?p ?_o
?p <rdfs:domain> ?dom
->
?s <rdf:type> ?dom
@test
?p != <rdf:type>


# added second clause (HUK)
$rdfs3
&priority 90
?_s ?p ?o
?p <rdf:type> <owl:ObjectProperty>
?p <rdfs:range> ?rng
->
?o <rdf:type> ?rng


# NOTE: redundant rule -- implemented through owl:TransitiveProperty
#$rdfs5
#?p <rdfs:subPropertyOf> ?q
#?q <rdfs:subPropertyOf> ?r
#->
#?p <rdfs:subPropertyOf> ?r
#@test
#?p != ?q
#?p != ?r
#?q != ?r


$rdfs6
?p <rdf:type> <rdf:Property>
->
?p <rdfs:subPropertyOf> ?p


# note: if we drop the test, we can avoid $rdfs6 (!?)
$rdfs7
&priority 30
?s ?p ?o
?p <rdfs:subPropertyOf> ?q
->
?s ?q ?o
@test
?p != ?q


# replaced rdfs:Class by owl:Class;
# or we add a further axiom that owl:Class is a subclass of rdfs:Class as it is
# enforced for OWL Lite and OWL DL
# NOTE: this rule only implements rdfs10 (note that ?c is a subclass of rdfs:Resource)
$rdfs8_10
?c <rdf:type> <owl:Class>
->
?c <rdfs:subClassOf> ?c


# universal instantiation
$rdfs9
&priority 10
?i <rdf:type> ?c
?c <rdfs:subClassOf> ?d
->
?i <rdf:type> ?d
@test
?c != ?d


# NOTE: redundant rule -- implemented through owl:TransitiveProperty
# NOTE: no rule defined for rdfs:subPropertyOf -- again redundant through
#       the characterization as being a transitive property
#$rdfs11
#?c <rdfs:subClassOf> ?d
#?d <rdfs:subClassOf> ?e
#->
#?c <rdfs:subClassOf> ?e
#@test
#?c != ?d
#?c != ?e
#?d != ?e


# NOTE: $rdfs12 omitted -- deals with rdfs:member
# will not fire for our OWL ontologies


# NOTE: $rdfs13 omitted -- deals with rdfs:Literal
# will not fire for our OWL ontologies


$owl_invOf
&priority 100
?s ?p ?o
?p <owl:inverseOf> ?q
->
?o ?q ?s


$owl_invOfBySymProp
?p <rdf:type> <owl:SymmetricProperty>
->
?p <owl:inverseOf> ?p


$owl_SymPropByInverse
?p <owl:inverseOf> ?p
->
?p <rdf:type> <owl:SymmetricProperty>


# NOTE: was _*not*_ part of OWLIM's OWL-Horst pie file
$owl_SymProp
&priority 160
?p <rdf:type> <owl:SymmetricProperty>
?s ?p ?o
->
?o ?p ?s


$owl_TransProp
&priority 20
?p <rdf:type> <owl:TransitiveProperty>
?x ?p ?y
?y ?p ?z
->
?x ?p ?z
@test
?x != ?y
?y != ?z


# second clause had been added to guarantee proper application context;
# note: a further rule, dealing with datatype properties is part of idefault.rdl
$owl_FunctPropObject
&priority 50
?p <rdf:type> <owl:FunctionalProperty>
?p <rdf:type> <owl:ObjectProperty>
?x ?p ?y
?x ?p ?z
->
?y <owl:sameAs> ?z
@test
?y != ?z
?p != <rdf:type>


# NOT part of the original rule set, a kind of integrity constraint, deriving
# that an individual is of type owl:Nothing;
# NOTE:  currently commented out  !!!!!!!!!!!!!!
#$owl_FunctPropDatatype
#?p <rdf:type> <owl:FunctionalProperty>
#?p <rdf:type> <owl:DatatypeProperty>
#?x ?p ?y
#?x ?p ?z
#->
#?x <rdf:type> <owl:Nothing>
#@test
#?y != ?z
#?p != <rdf:type>


# holds for both datatype and object properties
$owl_InvFunctProp
?p <rdf:type> <owl:InverseFunctionalProperty>
?y ?p ?x
?z ?p ?x
->
?y <owl:sameAs> ?z
@test
?y != ?z
?p != <rdf:type>


$owl_sameAsCopySubj
&priority 110
?x <owl:sameAs> ?y
?x ?p ?z
->
?y ?p ?z
@test
?x != ?y


# called owl_sameAsCopyPred in the original file, but owl:sameAs can NOT
# be applied to predicates, so replace sameAs by owl:equivalentProperty
# NOTE: original rule was never applied -- there are no properties p and q
#       that are connected by owl:sameAs
$owl_equivalentPropertyCopyPred
&priority 130
?p <owl:equivalentProperty> ?q
?x ?p ?y
->
?x ?q ?y
@test
?p != ?q


$owl_sameAsCopyObj
&priority 120
?x <owl:sameAs> ?y
?z ?p ?x
->
?z ?p ?y
@test
?x != ?y


$owl_EquivClassBySubClass
?x <rdfs:subClassOf> ?y
?y <rdfs:subClassOf> ?x
->
?x <owl:equivalentClass> ?y
@test
?x != ?y


$owl_EquivPropBySubProp
?p <rdfs:subPropertyOf> ?q
?q <rdfs:subPropertyOf> ?p
->
?p <owl:equivalentProperty> ?q
@test
?p != ?q


$owl_typeByAllVal
&priority 60
?x <owl:onProperty> ?p
?u <rdf:type> ?x
?x <owl:allValuesFrom> ?y
?u ?p ?v
->
?v <rdf:type> ?y


$owl_typeByHasVal
&priority 150
?r <owl:onProperty> ?p
?r <owl:hasValue> ?v
?i ?p ?v
->
?i <rdf:type> ?r


$owl_AttrByHasVal
&priority 180
?x <owl:hasValue> ?y
?x <owl:onProperty> ?p
?u <rdf:type> ?x
->
?u ?p ?y


$owl_typeBySomeVal
&priority 70
?q <rdf:type> ?c
?r <owl:onProperty> ?p
?r <owl:someValuesFrom> ?c
?i ?p ?q
->
?i <rdf:type> ?r


$owl_subClassByInters1
?c <owl:intersectionOf> ?x
?x <rdf:first> ?y
->
?c <rdfs:subClassOf> ?x
?c <rdfs:subClassOf> ?y


$owl_subClassByInters2
?x <rdf:rest> ?y
?c <owl:intersectionOf> ?x
->
?x <owl:intersectionOf> ?y
@test
?y != <rdf:nil>


$owl_typeByIntersect_1
&priority 170
?b <rdf:first> ?c
?b <rdf:rest> <rdf:nil>
?z <owl:intersectionOf> ?b
?i <rdf:type> ?c
->
?i <rdf:type> ?b


$owl_typeByIntersect_2
&priority 40
?b <rdf:first> ?c
?i <rdf:type> ?c
?i <rdf:type> ?b
?n <owl:intersectionOf> ?b
->
?i <rdf:type> ?n
